prontera,193,247,3	script	เกรวี่	10078,{
	setarray .@indices[1],EQI_HEAD_TOP,EQI_HEAD_MID,EQI_HEAD_LOW,EQI_ARMOR,EQI_HAND_L,EQI_HAND_R,EQI_GARMENT,EQI_SHOES,EQI_ACC_L,EQI_ACC_R,EQI_COSTUME_HEAD_TOP,EQI_COSTUME_HEAD_MID,EQI_COSTUME_HEAD_LOW,EQI_SHADOW_ARMOR,EQI_SHADOW_WEAPON,EQI_SHADOW_SHIELD,EQI_COSTUME_GARMENT,EQI_SHADOW_SHOES,EQI_SHADOW_ACC_L,EQI_SHADOW_ACC_R;
	setarray .@equipLocations[1],256,512,1,16,32,2,4,64,128,8,1024,2048,4096,65536,131072,262144,8192,524288,2097152,1048576;
	for(.@i = 1; .@i < getarraysize(.@indices); ++.@i){
		if(getequipisequiped(.@indices[.@i])
				&& getenchantgrade(.@indices[.@i]) < 4){
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]);
			.@isEquipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	
	// Equipment checking
	if (!.@isEquipped){
		callfunc("CutIn",.npcNormalFacials$[rand(getarraysize(.npcNormalFacials$))]);
		mes .npcName$;
		mes NpcMessageColor("ข้าไม่เห็นท่านกำลังสวมใส่ อุปกรณ์สวมใส่ ใด ๆ อยู่เลย..");
		next;
		callfunc("Exit");
	}
	
	callfunc("CutIn",.npcNormalFacials$[rand(getarraysize(.npcNormalFacials$))]);
	mes .npcName$;
	mes NpcMessageColor("โปรดเลือกอุปกรณ์สวมใส่");
	next;
	
	// Equipment selection
	.@menuSelected = select(.@menu$);
	gradePartSelected = .@indices[.@menuSelected];
	gradeItemIdSelected = getequipid(gradePartSelected);
	gradeEquipLocationSelected = .@equipLocations[.@menuSelected];
	
	// Equipment checking
	if(!getequipisequiped(gradePartSelected)){
		callfunc("CutIn",.npcNormalFacials$[rand(getarraysize(.npcNormalFacials$))]);
		mes .npcName$;
		mes NpcMessageColor("ท่านเลือกช่องที่ ไม่ได้สวมใส่อะไร.. อยู่เลยหนิ?");
		next;
		callfunc("Exit");
	}
	
	// Equipment checking
	if (callfunc("F_IsEquipIDHack",gradePartSelected,gradeItemIdSelected)){
		callfunc("CutIn",.npcNormalFacials$[rand(getarraysize(.npcNormalFacials$))]);
		mes .npcName$;
		mes NpcMessageColor("เอ๋? ท่านสลับอุปกรณ์สวมใส่ ระหว่างการดำเนินการหนิ!?");
		next;
		callfunc("Exit");
	}
	
	// Upgrade mode selection
	callfunc("CutIn",.npcNormalFacials$[rand(getarraysize(.npcNormalFacials$))]);
	mes .npcName$;
	.@maxGrade = getarraysize(.gradeCostItemIds);
	for(.@i = 0; .@i < .@maxGrade; .@i++)
	mes NpcMessageColor("Grade " + GetGradeName(.@i + 1) + " ต้องการ " + F_InsertComma(.gradeCostItemAmount[.@i]) + " " + getitemname(.gradeCostItemIds[.@i]));
	next;
	.@upgradeMode = select("ทีละขั้น:ทีเดียวเกรด A:ยกเลิก");
	if(.@upgradeMode == 3)
	callfunc("Exit");
	
	// Get current grade
	.@currentGrade = getenchantgrade(gradePartSelected);
	
	.@loopAmount = (.@upgradeMode == 1) ? 1 : (4 - .@currentGrade);
	
	
	// Loop
	for(.@i = 0; .@i < .@loopAmount; .@i++){
		// Item checking
		if(countitem(.gradeCostItemIds[.@currentGrade]) < .gradeCostItemAmount[.@currentGrade]){
			callfunc("CutIn",.npcNormalFacials$[rand(getarraysize(.npcNormalFacials$))]);
			mes .npcName$;
			mes NpcMessageColor("ต้องการ " + F_InsertComma(.gradeCostItemAmount[.@currentGrade]) + " " + getitemname(.gradeCostItemIds[.@currentGrade]));
			next;
			callfunc("Exit");
		}
		
		delitem .gradeCostItemIds[.@currentGrade],.gradeCostItemAmount[.@currentGrade];
		atcommand "@grade " + gradeEquipLocationSelected + " +1";
		.@currentGrade++;
	}
	
	specialeffect2 454;
	callfunc("CutIn",.npcActionFacials$[rand(getarraysize(.npcActionFacials$))]);
	mes .npcName$;
	mes getitemname(getequipid(gradePartSelected)) + " Grade " + GetGradeName(.@currentGrade);
	next;
	callfunc("Exit");
	
OnInit:
	// Grade D, C, B, A
	setarray .gradeCostItemIds,10000040,10000040,10000040,10000040;
	setarray .gradeCostItemAmount,30,300,3000,9000;
	// Name Setup
	.npcName$ = NpcNameColor(strnpcinfo(0));
	// Cut-in Setup
	setarray .npcNormalFacials$,"ep15_2_brt_1","ep15_2_brt_2","ep15_2_brt_4","ep15_2_brt_5","ep15_2_brt_6";
	setarray .npcActionFacials$,"ep15_2_brt_3","ep15_2_brt_7";
	// Information
	waitingroom "เพิ่ม Grade ให้อุปกรณ์สวมใส่",0;
	end;
}
